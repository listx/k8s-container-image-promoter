load("@io_bazel_rules_go//go:def.bzl", "go_binary", "go_library")
load("@io_bazel_rules_docker//go:image.bzl", "go_image")
load("@io_bazel_rules_docker//container:container.bzl", "container_bundle", "container_image", "container_layer", "container_push")
load("@io_bazel_rules_docker//contrib:push-all.bzl", "docker_push")
load("@package_bundle//file:packages.bzl", "packages")
load("@io_bazel_rules_docker//docker/toolchain_container:toolchain_container.bzl", "toolchain_container")

# Go image. "bazel build //:cip-docker-image.tar" results in a tarfile that is
# "docker load -i"-able, but the image is automatially named
# "bazel:cip-docker-image". To get around this, we use the container_bundle rule
# to set the image name directly.
go_image(
    name = "cip-docker-image",
    embed = [":go_default_library"],
    importpath = "sigs.k8s.io/k8s-container-image-promoter",
    goarch = "amd64",
    goos = "linux",
    pure = "on",
    base = "@google-sdk-base//image",
)

container_image(
    name = "cip-with-gcloud",
    base = ":cip-docker-image",
    entrypoint = [
        "/bin/bash",
        "-c",
    ],
    layers = [":cip-util"],
    env = {"PATH": "/cip:/cip/util:${PATH}"},
    symlinks = {"/cip/cip": "/app/cip-docker-image.binary"},
)

container_layer(
    name = "cip-util",
    directory = "/cip/util",
    files = ["util/multirun.sh"],
)

# Invoke with "bazel build //:cip-docker-loadable.tar". Then you can run "docker
# load -i bazel-bin/cip-docker-loadable.tar" to load it into the local Docker
# daemon.
container_bundle(
    name = "cip-docker-loadable",
    images = {
        "{STABLE_IMG_REGISTRY}/{STABLE_IMG_REPOSITORY}/{STABLE_IMG_NAME}:latest": "//:cip-with-gcloud",
        "{STABLE_IMG_REGISTRY}/{STABLE_IMG_REPOSITORY}/{STABLE_IMG_NAME}:{IMG_TAG}": "//:cip-with-gcloud",
    },
)

container_bundle(
    name = "e2e-env-docker-loadable",
    images = {
        "{STABLE_IMG_REGISTRY}/{STABLE_IMG_REPOSITORY}/cip-e2e-env:latest": "//:e2e-env",
        "{STABLE_IMG_REGISTRY}/{STABLE_IMG_REPOSITORY}/cip-e2e-env:{IMG_TAG}": "//:e2e-env",
    },
)

toolchain_container(
    name = "e2e-env-base",
    base = "@bazel-docker-gcloud//image",
    cmd = [
        "/bin/sh",
        "-c",
        "/bin/bash",
    ],
    env = {
        "LANG": "C.UTF-8",
        "LANGUAGE": "C.UTF-8",
        "LC_ALL": "C.UTF-8",
        "PATH": "$$PATH:/usr/local/go/bin",
    },
    # Remove non-deterministic files.
    installation_cleanup_commands = "rm -f /etc/init.d/.depend.boot /etc/init.d/.depend.start /etc/init.d/.depend.stop",
    language_layers = [
        "@layer_definitions//layers/ubuntu1604/go:go-ltl",
    ],
)

container_image(
    name = "e2e-env",
    base = "e2e-env-base",
    debs = [
        packages["make"],
    ],
)

docker_push(
    name = "push-e2e-env",
    bundle = "e2e-env-docker-loadable",
)

load("@bazel_gazelle//:def.bzl", "gazelle")

# gazelle:prefix sigs.k8s.io/k8s-container-image-promoter
gazelle(name = "gazelle")

go_library(
    name = "go_default_library",
    srcs = ["cip.go"],
    importpath = "sigs.k8s.io/k8s-container-image-promoter",
    visibility = ["//visibility:private"],
    x_defs = {
        "main.GitCommit": "{STABLE_GIT_COMMIT}",
        "main.GitDescribe": "{STABLE_GIT_DESC}",
        "main.TimestampUtcRfc3339": "{TIMESTAMP_UTC_RFC3339}",
    },
    deps = [
        "//lib/dockerregistry:go_default_library",
        "//lib/stream:go_default_library",
        "@io_k8s_klog//:go_default_library",
    ],
)

go_binary(
    name = "cip",
    embed = [":go_default_library"],
    visibility = ["//visibility:public"],
)

docker_push(
    name = "push-cip",
    bundle = "cip-docker-loadable",
)
